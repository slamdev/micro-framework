plugins {
    id 'base'
    id 'idea'
}

ext {
    libs = subprojects.collect { it.name }.findAll { !(it in ['bom', 'modules']) }
}

allprojects {
    group 'com.github.slamdev.microframework'
}

subprojects {
    afterEvaluate { module ->
        if (module.plugins.hasPlugin('io.spring.dependency-management')) {
            dependencyManagement {
                dependencies {
                    dependency 'com.google.guava:guava:23.0'
                    dependency 'com.google.code.gson:gson:2.8.1'
                    dependency 'com.squareup.okhttp3:okhttp:3.8.1'
                    dependency 'org.easybatch:easybatch-core:5.1.0'
                    dependency 'org.easybatch:easybatch-jdbc:5.1.0'
                    dependency 'org.easybatch:easybatch-flatfile:5.1.0'
                    dependency 'org.easybatch:easybatch-json:5.1.0'
                    dependency 'io.undertow:undertow-core:1.4.18.Final'
                    dependency 'com.typesafe:config:1.3.1'
                    dependency 'org.slf4j:slf4j-api:1.7.25'
                    dependency 'ch.qos.logback:logback-classic:1.2.3'
                    dependency 'ch.qos.logback:logback-access:1.2.3'
                    dependency 'org.projectlombok:lombok:1.16.18'
                    dependency 'org.jdbi:jdbi:2.78'
                    dependency 'org.flywaydb:flyway-core:4.2.0'
                    dependency 'com.google.guava:guava:23.0'
                    dependency 'com.zaxxer:HikariCP:2.6.3'
                    dependency 'com.h2database:h2:1.4.196'
                    dependency 'org.assertj:assertj-core:3.8.0'
                    dependency 'junit:junit:4.12'
                    dependency 'org.mockito:mockito-core:2.9.0'
                    dependency 'org.postgresql:postgresql:42.1.4'
                    dependency 'org.slf4j:jul-to-slf4j:1.7.25'
                    dependency 'com.jayway.jsonpath:json-path:2.4.0'
                    dependency 'com.google.cloud:google-cloud-storage:1.4.0'
                }
            }
        }
        if (module.plugins.hasPlugin('maven-publish')) {
            if (module.name != 'bom') {
                javadoc {
                    source = sourceSets.main.allJava
                }

                task sourceJar(type: Jar) {
                    from sourceSets.main.allJava
                    classifier 'sources'
                }

                task javadocJar(type: Jar) {
                    from javadoc
                    classifier 'javadoc'
                }

                artifacts {
                    archives jar
                    archives sourceJar
                    archives javadocJar
                }

                publishing {
                    publications {
                        "$module.name"(MavenPublication) {
                            from components.java
                            artifact sourceJar
                            artifact javadocJar
                        }
                    }
                }
            }
        }
        if (module.plugins.hasPlugin('com.jfrog.bintray')) {
            bintray {
                user = 'slamdev'
                key = project.properties['bintray.publish.key']
                override = true
                publications = [module.name]
                pkg {
                    repo = 'maven'
                    name = module.name
                    desc = 'Micro Framework'
                    licenses = ['MIT']
                    websiteUrl = 'https://github.com/slamdev/micro-framework'
                    issueTrackerUrl = 'https://github.com/slamdev/micro-framework/issues'
                    vcsUrl = 'https://github.com/slamdev/micro-framework.git'
                    labels = ['java', 'microsevices', 'framework']
                }
            }
        }
    }
}
