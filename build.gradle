plugins {
    id 'base'
    id 'idea'
}

ext {
    libs = subprojects.collect { it.name }.findAll { !(it in ['bom', 'modules']) }
}

allprojects {
    group 'com.github.slamdev.microframework'
}

subprojects {
    afterEvaluate { module ->
        if (module.plugins.hasPlugin('io.spring.dependency-management')) {
            dependencyManagement {
                dependencies {
                    dependency 'com.google.guava:guava:23.0'
                    dependency 'com.squareup.moshi:moshi:1.5.0'
                    dependency 'com.squareup.okhttp3:okhttp:3.8.1'
                    dependency 'org.easybatch:easybatch-core:5.1.0'
                    dependency 'io.undertow:undertow-core:1.4.18.Final'
                    dependency 'com.typesafe:config:1.3.1'
                    dependency 'org.slf4j:slf4j-api:1.7.25'
                    dependency 'ch.qos.logback:logback-classic:1.2.3'
                    dependency 'ch.qos.logback:logback-access:1.2.3'
                    dependency 'org.projectlombok:lombok:1.16.18'
                    dependency 'org.jdbi:jdbi:2.78'
                }
            }
        }
        if (module.plugins.hasPlugin('maven-publish') && module.name != 'bom') {
            publishing {
                publications {
                    "$project.name"(MavenPublication) {
                        from components.java
                    }
                }
            }
        }
    }
}
